#!/usr/bin/env zsh
# shellcheck shell=bash

#
# ~/.zshrc: sourced in interactive shells
#
#  - Should set up options, key bindings, functions, aliases, etc.
#
# Guides:
#  - https://zsh.sourceforge.io/Intro/intro_3.html
#  - https://zsh.sourceforge.io/Guide/zshguide02.html
#
# Examples:
#  - https://github.com/zsh-users/zsh/blob/master/StartupFiles/zshrc
#  - https://github.com/marlonrichert/zsh-launchpad/tree/main
#  - https://github.com/romkatv/zsh4humans/blob/v5/.zshrc
#  - https://github.com/romkatv/zsh4humans/blob/v5/.zshrc.mac
#

# @note Uncomment the following line to profile startup time.
# zmodload zsh/zprof

# Load shared helpers (required by profile.sh).
rchelpers="${XDG_CONFIG_HOME:-$HOME/.config}/shell/helpers.sh"
# shellcheck disable=1090
if [ ! -r "$rchelpers" ] || ! source "$rchelpers"; then
    printf '[error] %s\n' "file not loaded: $rchelpers" >&2
    return 1
fi
log:debug "file loaded: $rchelpers"
unset rchelpers

# Load shared shell profile.
source:file "$XDG_CONFIG_HOME/shell/profile.sh"

#
# Options
#
#  - Reference: https://zsh.sourceforge.io/Doc/Release/Options.html
#  - Examples
#    - https://github.com/willghatch/zsh-saneopt/tree/master
#    - https://github.com/sorin-ionescu/prezto/tree/master/modules
#

# Correctly display UTF-8 with combining characters.
if [[ "$(locale LC_CTYPE)" == "UTF-8" ]]; then
  setopt COMBINING_CHARS
fi

# Disable the log builtin, so we don't conflict with /usr/bin/log
disable log

# Use extended glob patterns.
setopt extendedglob
# Include hidden files.
setopt globdots
# Remove superfluous blanks before recording entry.
setopt histreduceblanks
# No beeping
unsetopt beep
# No c-s/c-q output freezing
unsetopt flowcontrol
# Try to avoid the 'zsh: no matches found...'
unsetopt nomatch

export HISTSIZE=10000
export SAVEHIST=10000

#
# Path
#

# Ensure path arrays do not contain duplicates.
# shellcheck disable=2034
typeset -gU cdpath fpath mailpath manpath path

# Register user funcs and autoload them to make them callable. They can also be
# invoked as scripts.
# https://zsh.sourceforge.io/Intro/intro_4.html
# @todo load last, but need plugin helpers early
# shellcheck disable=2206
fpath=($XDG_CONFIG_HOME/shell/zsh/functions $fpath)
autoload -Uz "$XDG_CONFIG_HOME/shell/zsh/functions"/*(.:t)

#
# Instant prompt
#

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
#
# Activate direnv here for compatibility:
# https://github.com/romkatv/powerlevel10k/blob/master/README.md#how-do-i-initialize-direnv-when-using-instant-prompt
#
# @note tmux sessions should be created after instant prompt because tmux
# generates output to parent process:
# https://github.com/romkatv/powerlevel10k/issues/1203
# shellcheck disable=2296
p10k_cache="${XDG_CACHE_HOME}/p10k-instant-prompt-${(%):-%n}.zsh"
run:if-cmd direnv emulate zsh -c "$(direnv export zsh)"
source:file "$p10k_cache"
run:if-cmd direnv emulate zsh -c "$(direnv hook zsh)"

#
# Completions
#

# Use fuzzy searching for history substring search
export HISTORY_SUBSTRING_SEARCH_FUZZY=1

# homebrew
if [ -n "$HOMEBREW_PREFIX" ]; then
  # Register homebrew completions so they'll be loaded by the completion plugin.
  #  - completions/zsh: completions for the brew command
  #  - share/zsh/site-functions: completions for commands installed via brew
  #  - share/bash-completion/completions: bash completions in zsh format (in
  #    case one isn't defined in zsh/site-functions)
  #
  # @todo this should be done by the completion plugin, maybe try updating it?
  # @todo I needed to run these commands to fix zcompinit security issues from
  # docker completions:
  # compaudit | xargs chown -R $(whoami)
  # compaudit | xargs chmod go-w
  fpath+=("$HOMEBREW_PREFIX"/share/bash-completion/completions)
  # shellcheck disable=1036,2128,2206
  fpath=(
    "$HOMEBREW_PREFIX"/completions/zsh(/N)
    "$HOMEBREW_PREFIX"/share/zsh/site-functions(/N)
    /usr/share/zsh/site-functions(/N)
    $fpath
  )

  # The git completions installed by brew are worse than the default ones that
  # ship with zsh, so disable them by removing their file permissions. The
  # external completions frome felipec take precendence either way, but this is
  # the appropriate fallback.
  for f in _git git-completion.bash; do
    if [ -r "$HOMEBREW_PREFIX/share/zsh/site-functions/$f" ]; then
      chmod 0 "$HOMEBREW_PREFIX/share/zsh/site-functions/$f"
    fi
  done
fi

# Load bash completions because there might be some that zsh doesn't have. We
# load these before prezto so the completion plugin will override any
# conflicting definitions, because zsh completions are usually better.
autoload -Uz bashcompinit
if ! bashcompinit; then
  log:warn "failed to load bash completions"
fi

# gcloud
if [ -d "$HOMEBREW_PREFIX" ]; then
    source:file "$HOMEBREW_PREFIX/share/google-cloud-sdk/path.zsh.inc"
    source:file "$HOMEBREW_PREFIX/share/google-cloud-sdk/completion.zsh.inc"
fi

# ngrok
eval:if-cmd ngrok ngrok completion

# zsh-autocomplete: this config must be set before loading the plugin and cannot
# be changed at runtime.
zstyle ':autocomplete:*' widget-style menu-select
# complete-word: (Shift-)Tab inserts the top (bottom) completion.
# menu-complete: Press again to cycle to next (previous) completion.
# menu-select:   Same as `menu-complete`, but updates selection in menu.

#
# Plugins
#

source:file "$XDG_CONFIG_HOME/shell/zsh/plugins.zsh"
# Load p10k immediately after plugins for a fast prompt.
# https://github.com/romkatv/zsh-bench
source:file "$XDG_CONFIG_HOME/shell/zsh/p10k.zsh"
source:file "$XDG_CONFIG_HOME/shell/shellrc.sh"

# fzf
# https://thevaluable.dev/fzf-shell-integration/
source:file "$FZF_DIR/shell/completion.zsh" # @audit-ok
source:file "$FZF_DIR/shell/key-bindings.zsh" # @audit-ok
# source:file "$XDG_CONFIG_HOME/shell/zsh/fzf-tab-rc.zsh"

# zsh-autocomplete
source:file "$XDG_CONFIG_HOME/shell/zsh/zsh-autocomplete-rc.zsh"

# cdr
# https://zsh.sourceforge.io/Doc/Release/User-Contributions.html#Recent-Directories
autoload -Uz cdr chpwd_recent_dirs add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
# zstyle ':completion:*:*:cdr:*:*' menu selection

# zoxide (load after compinit, fzf-rc, zsh-autocomplete)
export _ZO_FZF_OPTS="$FZF_DEFAULT_OPTS $FZF_ALT_C_OPTS"
eval:if-cmd zoxide zoxide init zsh # @audit-ok

#
# Completions
#

# Only show completions for exact matches.
unsetopt completeinword
# Complete globs instead of inserting them. The expansion will be shown in the
# zsh-autocomplete menu anyway.
setopt globcomplete

# Override the match (ma) color to be more subtle--fg white, bg gray, bold--to
# match our fzf theme.
# @todo update based on new fzf theme
zstyle ':completion:*' list-colors "ma=38;5;251;48;5;237;1"

# ignore completion to functions starting with _
zstyle ':completion:*:functions' ignored-patterns '_*'
# menu selection with a cursor will be used when the number of possible matches doesn't fit the screen
zstyle ':completion:*' menu select=long
# menu selection with a cursor will be used for git commands
zstyle ':completion:*:git*:*' menu select=2

source:file "$XDG_CONFIG_HOME/shell/zsh/completions-rc.zsh"

#
# vim-surround
#
# https://thevaluable.dev/zsh-install-configure-mouseless/
#

autoload -Uz surround

zle -N add-surround surround
zle -N change-surround surround
zle -N delete-surround surround

bindkey -M vicmd ys add-surround
bindkey -M visual S add-surround
bindkey -M vicmd cs change-surround
bindkey -M vicmd ds delete-surround

#
# zsh-bracketed
#
# https://github.com/zsh-users/zsh/blob/master/Functions/Zle/select-quoted
#

autoload -Uz select-quoted
zle -N select-quoted
for m in visual viopp; do
  for c in {a,i}{\',\",\`}; do
    bindkey -M $m $c select-quoted
  done
done

#
# zsh-quoted
#
# https://github.com/zsh-users/zsh/blob/master/Functions/Zle/select-bracketed
#

autoload -Uz select-bracketed
zle -N select-bracketed
for m in visual viopp; do
  # shellcheck disable=2296
  for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
    bindkey -M $m $c select-bracketed
  done
done

#
# builtin functions
#

# https://github.com/rothgar/mastering-zsh/blob/master/docs/helpers/widgets.md
autoload -Uz tetris tetriscurses

#
# Keybinds
#

source:file "$XDG_CONFIG_HOME/shell/zsh/keybinds.zsh"

#
# Aliases
#

source:file "$XDG_CONFIG_HOME/shell/zsh/aliases.zsh"

# @note Uncomment the following line to print startup time.
# zprof
